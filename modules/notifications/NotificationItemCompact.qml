pragma ComponentBehavior: Bound

import qs.ds
import qs.ds.icons as Icons
import qs.ds.text as DsText
import qs.ds.animations
import qs.services
import Quickshell
import Quickshell.Widgets
import Quickshell.Services.Notifications
import QtQuick
import QtQuick.Layouts

Rectangle {
    id: root

    required property var notification
    required property int notificationWidth
    property bool showDismiss: false

    readonly property int margin: Foundations.spacing.xs
    readonly property int imageDimension: 32
    readonly property int previewHeight: 120
    property real previewAspect: 0

    readonly property string image: notification.image ?? ""
    readonly property string appIcon: notification.appIcon ?? ""
    readonly property bool appIconIsScreenshot: root.appIcon.startsWith("file://") && (notification.appName ?? "") === "niri"
    readonly property string previewSource: root.image.startsWith("file://") ? root.image : (root.appIconIsScreenshot ? root.appIcon : "")
    readonly property bool hasPreview: previewSource !== ""
    readonly property bool hasImage: image !== "" && !hasPreview
    readonly property string summary: notification.summary ?? ""
    readonly property string body: notification.body ?? ""
    readonly property bool isCritical: notification.urgency === NotificationUrgency.Critical

    color: {
        if (mouseArea.hasFeedback) return mouseArea.feedbackColor;
        if (mouseArea.containsMouse) return GtkTheme.controlBg;
        return "transparent";
    }
    radius: Foundations.radius.xs
    implicitWidth: notificationWidth
    implicitHeight: content.implicitHeight + margin * 2

    Behavior on color {
        BasicColorAnimation {
            duration: Foundations.duration.fast
        }
    }

    NotificationClickArea {
        id: mouseArea

        anchors.fill: parent
        notification: root.notification
    }

    ColumnLayout {
        id: content

        anchors.fill: parent
        anchors.margins: margin
        spacing: Foundations.spacing.xs

        RowLayout {
            id: contentRow

            Layout.fillWidth: true
            spacing: Foundations.spacing.s

            // Image/icon
            Loader {
                active: root.hasImage
                Layout.preferredWidth: root.imageDimension
                Layout.preferredHeight: root.imageDimension
                Layout.alignment: Qt.AlignTop

                sourceComponent: ClippingRectangle {
                    color: "transparent"
                    radius: Foundations.radius.xs

                    Image {
                        anchors.fill: parent
                        asynchronous: true
                        cache: false
                        fillMode: Image.PreserveAspectCrop
                        source: Qt.resolvedUrl(root.notification.image)
                    }
                }
            }

            // Text content
            ColumnLayout {
                Layout.fillWidth: true
                spacing: 2

                DsText.BodyM {
                    Layout.fillWidth: true
                    text: summaryMetrics.elidedText
                    maximumLineCount: 1
                    color: root.isCritical ? Foundations.palette.base08 : GtkTheme.contentText
                }

                TextMetrics {
                    id: summaryMetrics
                    elide: Text.ElideRight
                    elideWidth: root.notificationWidth - root.imageDimension - root.margin * 4 - (root.showDismiss ? 24 : 0)
                    font.family: Foundations.font.family.sans
                    font.pointSize: Foundations.font.size.m
                    text: root.summary
                }

                DsText.BodyS {
                    Layout.fillWidth: true
                    text: bodyMetrics.elidedText
                    maximumLineCount: 2
                    wrapMode: Text.WrapAtWordBoundaryOrAnywhere
                    color: GtkTheme.contentText
                    visible: root.body !== ""
                }

                TextMetrics {
                    id: bodyMetrics
                    elide: Text.ElideRight
                    elideWidth: root.notificationWidth - root.imageDimension - root.margin * 4 - (root.showDismiss ? 24 : 0)
                    font.family: Foundations.font.family.sans
                    font.pointSize: Foundations.font.size.s
                    text: root.body.replace(/\n/g, " ")
                }
            }

            // Dismiss button (shows on row hover)
            Rectangle {
                id: dismissButton

                visible: root.showDismiss
                opacity: mouseArea.containsMouse ? 1 : 0
                Layout.preferredWidth: 20
                Layout.preferredHeight: 20
                Layout.alignment: Qt.AlignVCenter
                radius: 10
                color: dismissArea.containsMouse ? Foundations.palette.base08 : GtkTheme.controlBg

                Behavior on opacity {
                    BasicNumberAnimation {
                        duration: Foundations.duration.fast
                    }
                }

                Icons.MaterialFontIcon {
                    anchors.centerIn: parent
                    text: "close"
                    color: GtkTheme.contentText
                    font.pointSize: Foundations.font.size.xs
                }

                MouseArea {
                    id: dismissArea
                    anchors.fill: parent
                    anchors.margins: -4
                    hoverEnabled: true
                    cursorShape: Qt.PointingHandCursor
                    onClicked: root.notification.dismiss()
                }
            }
        }
        Loader {
            id: compactPreview

            active: root.hasPreview
            Layout.fillWidth: true
            Layout.preferredHeight: root.hasPreview ? (root.previewAspect > 0 ? Math.min(root.previewHeight, Math.round(compactPreview.width / root.previewAspect)) : root.previewHeight) : 0
            asynchronous: true

            sourceComponent: ClippingRectangle {
                color: "transparent"
                radius: Foundations.radius.xs

                Image {
                    anchors.fill: parent
                    asynchronous: true
                    cache: false
                    fillMode: Image.PreserveAspectFit
                    source: root.previewSource
                    sourceSize.height: root.previewHeight * 2
                    onStatusChanged: {
                        if (status === Image.Ready && implicitHeight > 0)
                            root.previewAspect = implicitWidth / implicitHeight;
                    }
                }
            }
        }
    }
}
